group 'de.servicekommune'
version '1.0-SNAPSHOT'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.7.5"
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0' // creating fat jars with all dependencies
  }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"
apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow' // creating fat jars with all dependencies

repositories {
  jcenter()
}

dependencies {
  // database
  implementation 'org.mariadb.jdbc:mariadb-java-client:2.5.2'
  implementation 'org.flywaydb:flyway-core:6.2.0' // DB migrations / upgrades to schemas via Flyway
  testCompile 'com.h2database:h2:1.4.200' // In memory DB for tests

  // password hashing
  implementation 'org.mindrot:jbcrypt:0.4'

  // testing
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

  // logging
  runtime "org.slf4j:slf4j-simple:1.7.25"
}

// Show test results in console
test {
  testLogging {
    events "passed", "skipped", "failed"
  }
}

// Output fat jar to build/libs/sk-tracker-with-dependencies.jar
shadowJar {
  archiveBaseName.set('sk-tracker-with-dependencies')
  archiveClassifier.set('')
  archiveVersion.set('')
}
